apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  api libs.slf4j

  // please keep the list grouped by configuration and ordered by name
  implementation libs.commons.io
  implementation libs.commons.lang3
  implementation libs.commons.text
  implementation libs.commons.validator
  implementation libs.gson

  // shaded, but not relocated
  implementation project(':check-api')

  compileOnly libs.jsr305
  compileOnly libs.javax.servlet.api

  testImplementation libs.junit4
  testImplementation libs.junit5
  testImplementation project(':test-fixtures')
  testImplementation libs.guava
  testImplementation libs.junit.dataprovider
  testImplementation libs.assertj
  testImplementation libs.mockito

  testRuntimeOnly libs.jupiter.vintage.engine
  testRuntimeOnly libs.jupiter.engine
}

configurations {
  // Make the compileOnly dependencies available when compiling/running tests
  testImplementation.extendsFrom compileOnly
}

def on3Digits(version) {
  def projectversion3digits = version - ~/-\w+/
  projectversion3digits = projectversion3digits.tokenize('.').plus(0).take(3).join('.')
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
  filter ReplaceTokens, tokens: [
    // The build version is composed of 4 fields, including the semantic version and the build number provided by Travis.
    'project.buildVersion': project.version.endsWith('SNAPSHOT') ? project.version : on3Digits(project.version) + '.' + (System.getProperty("buildNumber") ?: '0'),
  ]
}

shadowJar {
  archivesBaseName = 'sonar-plugin-api'
  archiveClassifier = ''
  configurations = [project.configurations.runtimeClasspath]
  minimize {
    exclude(project(':check-api'))
  }
  relocate('com.google', 'org.sonar.api.internal.google')
  relocate('org.apache.commons', 'org.sonar.api.internal.apachecommons')
  dependencies {
    exclude(dependency('org.slf4j:slf4j-api:.*'))
  }
  exclude 'META-INF/**/module-info.class'
}

tasks["build"].dependsOn(shadowJar)

artifactoryPublish.skip = false

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'sonar-plugin-api'
      pom {
        name = 'Sonar Plugin API'
        description = project.description
        url = 'https://www.sonarqube.org/'
        organization {
          name = 'SonarSource'
          url = 'https://www.sonarsource.com/'
        }
        licenses {
          license {
            name = 'GNU LGPL 3'
            url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/SonarSource/sonar-plugin-api'
        }
        developers {
          developer {
            id = 'sonarsource-team'
            name = 'SonarSource Team'
          }
        }
      }
      artifact source: shadowJar, classifier: null
      artifact sourcesJar
      artifact javadocJar
      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', 'org.slf4j')
        dependencyNode.appendNode('artifactId', 'slf4j-api')
        dependencyNode.appendNode('version', libs.versions.slf4j.get())
      }
    }
  }
}

test {
  // Enabling the JUnit Platform (see https://github.com/junit-team/junit5-samples/tree/master/junit5-migration-gradle)
  useJUnitPlatform()
}
