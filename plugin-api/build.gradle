apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java-test-fixtures'

dependencies {
  api libs.slf4j

  // please keep the list grouped by configuration and ordered by name
  implementation libs.commons.io
  implementation libs.commons.lang
  implementation libs.gson

  // shaded, but not relocated
  implementation project(':check-api')

  compileOnly libs.logback.classic
  compileOnly libs.jsr305
  compileOnly libs.servlet.api

  testFixturesCompileOnly libs.jsr305
  testFixturesImplementation libs.junit4
  // Used by LogTesterJUnit5
  testFixturesImplementation libs.junit5

  testImplementation libs.junit4
  testImplementation libs.junit5
  testImplementation libs.guava
  testImplementation libs.junit.dataprovider
  testImplementation libs.assertj
  testImplementation libs.mockito
}

configurations {
  // Make the compileOnly dependencies available when compiling/running tests
  testImplementation.extendsFrom compileOnly
}

def on3Digits(version) {
  def projectversion3digits = version - ~/-\w+/
  projectversion3digits = projectversion3digits.tokenize('.').plus(0).take(3).join('.')
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
  filter ReplaceTokens, tokens: [
    // The build version is composed of 4 fields, including the semantic version and the build number provided by Travis.
    'project.buildVersion': project.version.endsWith('SNAPSHOT') ? project.version : on3Digits(project.version) + '.' + (System.getProperty("buildNumber") ?: '0'),
  ]
}

shadowJar {
  archivesBaseName = 'sonar-plugin-api'
  archiveClassifier = ''
  configurations = [project.configurations.runtimeClasspath]
  minimize {
    exclude(project(':check-api'))
  }
  relocate('com.google', 'org.sonar.api.internal.google')
  relocate('org.apache.commons', 'org.sonar.api.internal.apachecommons')
}

tasks["build"].dependsOn(shadowJar)

// only interested in shadowJar, but because of https://github.com/gradle/gradle/issues/19946 we should keep producing regular jar
jar.enabled = true
artifactoryPublish.skip = false

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'sonar-plugin-api'
      pom {
        name = 'Sonar Plugin API'
        description = project.description
        url = 'https://www.sonarqube.org/'
        organization {
          name = 'SonarSource'
          url = 'https://www.sonarsource.com/'
        }
        licenses {
          license {
            name = 'GNU LGPL 3'
            url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/SonarSource/sonar-plugin-api'
        }
        developers {
          developer {
            id = 'sonarsource-team'
            name = 'SonarSource Team'
          }
        }
      }
      artifact source: shadowJar, classifier: null
      artifact testFixturesJar
      artifact sourcesJar
      artifact javadocJar
    }
  }
}
